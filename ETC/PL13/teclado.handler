### ESTRUCTURA DE COMPUTADORES (ETS Eng. Inf.)
##  
## marzo de 2014
##
## PRÁCTICA 13: SINCRONIZACIÓN POR INTERRUPCIONES
##
## Código inicial del manejador


##############################################################
##              ÁREA DE DATOS DEL MANEJADOR                 ##
##############################################################

			.kdata
var1:		.word 0xFFFFFFFF
contexto:	.space 16		# Espacio para almacenar 4 palabras

#############################################################
##                  CÓDIGO DEL MANEJADOR                   ##
#############################################################

          .ktext 0x80000080

    ## Preserva el contexto del programa de usuario

	.set noat
	sw $at, 0($k1)
	.set at
	sw $t0, 4($k1)
	sw $a0, 8($k1)
	sw $v0, 12($k1)

    ## Identifica y trata excepciones

	# Identifica interrupción = Pulsar una tecla
	li $t0, 0xffff0000
	lw $a0, 4($t0)

	# Tratamiento = Imprimir un asterisco
	li $v0, 11
	li $a0, '*'
	syscall

    ## Restaura el contexto

	.set noat
	lw $at, 0($k1)
	.set at
	lw $t0, 4($k1)
	lw $a0, 8($k1)
	lw $v0, 12($k1)

    ## Restaura el modo usuario y vuelve al programa

	mfc0 $k0, $14 		# Guarda en $k0 la dirección de la instrucción afetada (contenida en $14)
	rfe
	jr $k0 

#############################################################
##                    CÓDIGO DE INICIO                     ##
#############################################################

          .text
          .globl __start 
__start: 
    ## Código de inicio

	la $k1, contexto

	li $t0, 0xffff0000
	li $t1, 2
	sb $t1, 0($t0)		# Bit E = 1
	
	li $t1, 0x103
	mtc0 $t1, $12		# IM0 = 1, KUC = 1 (modo usuario) y IEC = 1 (interrupciones habilitadas)
	
    ## Salto al programa de usuario
	jal main

    ## Código de terminación
	li $v0, 10
	syscall	 # Función exit

